WHITESPACE = _{ " " | "\t" }
//COMMENT = _{ ("'" | "REM") ~ metacommand? ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
comment = { remark_leader ~ metacommand? ~ (!"\n" ~ ANY)* }

program = { SOI ~ ((statement ~ comment? ~ statement_separator) | "\n")* ~ EOI }

statement = {
    assignment_statement |
    print_statement |
    input_statement |
    comment
}
statement_separator = _{ "\n" | ":" | EOI }

assignment_statement = { variable ~ assignment_operator ~ expression }
print_statement = { "PRINT" ~ expression }
input_statement = { "INPUT" ~ (string ~ (";" | ","))? ~ variable ~ ("," ~ variable)* }

expression = { term ~ ((addition_operator|subtraction_operator) ~ term)* }
term = { factor ~ ((multiplication_operator|division_operator) ~ factor)* }
factor = { number | string | variable | ("(" ~ expression ~ ")") }

metacommand = { "$STATIC" | "$DYNAMIC" }

number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? | "." ~ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
variable = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC)* ~ (single_precision_suffix | double_precision_suffix | string_suffix | integer_suffix | long_integer_suffix)? }


// Date type suffixes
single_precision_suffix = { "!" }
double_precision_suffix = { "#" }
string_suffix = { "$" }
integer_suffix = { "%" }
long_integer_suffix = { "&" }

// Mathemtical operators
multiplication_operator = { "*" }
subtraction_operator = { "-" }
division_operator = { "/" }
addition_operator = { "+" }
exponentiation_operator = { "^" }

// Logic operators
assignment_operator = { "=" }
relational_operators = { "<" | "=" | ">" }

remark_leader = { "'" | "REM" }